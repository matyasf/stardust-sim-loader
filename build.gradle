import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    ext {
        if (!project.hasProperty('repoManagerURL') ||
                !project.hasProperty('mavenUsername') ||
                !project.hasProperty('mavenPassword')) {
            println("NOTE: custom Maven repository is not configured in gradle.properties")
            repoManagerURL = "https://repo1.maven.org/maven2"
            mavenUsername = ""
            mavenPassword = ""
        }
        if (!project.hasProperty('flashPlayerLocation')) {
            println("NOTE: Flash player path is not configured in gradle.properties, tests will not work.")
            flashPlayerLocation = ""
        }
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url repoManagerURL + 'repo'
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
        }
    }

    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '1.5.0'
    }
}

apply plugin: 'gradlefx'
apply plugin: 'ideafx'
apply plugin: 'maven'

group = 'com.funkypandagame'
version = '3.31'
type = 'swc'

srcDirs	= ['src/main/flex']
resourceDirs = ['src/main/resources']
testDirs = ['src/test/flex']
testResourceDirs = ['src/test/resources']
playerVersion = '20.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://raw.githubusercontent.com/tconkling/maven-repo/master/"
    }
    maven {
        url repoManagerURL + 'repo'
        credentials {
            username = mavenUsername
            password = mavenPassword
        }
    }
    ivy {
        name 'Adobe Air SDK'
        artifactPattern  Os.isFamily(Os.FAMILY_WINDOWS) ?
                'http://airdownload.adobe.com/air/win/download/[revision]/[module]_Compiler.[ext]' :
                'http://airdownload.adobe.com/air/mac/download/[revision]/[module]_Compiler.[ext]'
    }
}

dependencies {
    airSDK group: 'com.adobe', name: 'AIRSDK', version: '20.0', ext: Os.isFamily(Os.FAMILY_WINDOWS) ? 'zip' : 'tbz2'

    external 'com.funkypandagame:stardust-library:3.28'

    test group: 'org.flexunit', name: 'flexunit-tasks', version: '4.2.0', ext: 'jar'
    test group: 'org.flexunit', name: 'flexunit-as3', version: '4.2.0', ext: 'swc'
    test group: 'org.flexunit', name: 'flexunit-aircilistener', version: '4.2.0', ext: 'swc'
    test group: 'org.flexunit', name: 'flexunit-cilistener', version: '4.2.0', ext: 'swc'
    test group: 'org.flexunit', name: 'flexunit-uilistener', version: '4.2.0', ext: 'swc'
}

def compilerOptions = [
        '-inline',
        // AS3 metadata
        '-keep-as3-metadata+=Inline,Embed,SWF,Transient'
]

additionalCompilerOptions = compilerOptions

sdkAutoInstall {
    showPrompts = false
}

flexUnit {
    additionalCompilerOptions = compilerOptions
    command = flashPlayerLocation
    template = buildDir.toString() + "/reports/FlexUnitRunnerTemplate.as"
}

///////////////////////////////////////// Wrapper
task wrapper(type: Wrapper) {
    group = 'Buildmaster'
    description = 'Generates gradlew and gradlew.bat bootstrap scripts.'
    gradleVersion = '3.2.1'
    // place jar file and properties into a subdirectory to avoid root dir clutter
    jarFile = 'gradle/wrapper/gradle.jar'
}

///////////////////////////////////////// Maven
// this is a workaround, see https://github.com/GradleFx/GradleFx/issues/91
uploadArchives {
    repositories {
        mavenDeployer {
            // invoke this with the -PdeployToRepoManager command line argument
            if (project.hasProperty('deployToRepoManager')) {
                repository(url: repoManagerURL + 'funkypanda') {
                    authentication(userName: mavenUsername, password: mavenPassword)
                }
            }
            else {
                repository url: mavenLocal().url
            }
            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                project.configurations["external"].allDependencies.each { Dependency dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                    dependencyNode.appendNode('type', 'swc')

                }
            }
        }
    }
}